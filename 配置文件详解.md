# 基于iVim的Vim插件配置

##  iVim的设置

### iVim  使用者信息

#### let g:ivim_email = 'iamhaiwei@foxmail.com' 
设置ivim使用者的邮箱，这里重点说一下这个g:代表的意思,在vim中存在一些内部的符号变量：
一个变量名由ASCII字母, 数字和下划线组成.`:let`可以在任意地方使用全局变量. 这意味着在一个脚本文件中引用的名为”count”的变量, 同样可以被另一个脚本文件所引用. 这样的应用至少会引起理解上的混乱, 实际情况往往更糟. 可以在变量名前前辍以”s:”使该变量局部有效于当前的脚本文件.删除变量 :unlet s:count

| 变量名称              | 变量缩写 | 作用             |
| ----------------- | ---- | -------------- |
| buffer-variable   | b:   | 局部于一个缓冲区的变量    |
| window-variable   | w:   | 局部于一个窗口的变量     |
| tabpage-variable  | t:   | 局部于一个当前标签页的变量  |
| global-variable   | g:   | 全局变量(同样适用于函数中) |
| local-variable    | l:   | 函数内的局部变量       |
| script-variable   | s:   | 变量局部有效于当前的脚本文件 |
| function-argument | a:   | 函数参数变量，只能用在函数中 |
| vim-variable      | v:   | Vim的预定义变量      |
这里就是自定义一个全局变量名字叫ivim_email，并且赋值为iamhaiwei@foxmail.com

#### let g:ivim_github='https://github.com/HaiweiCode' 
自定义一个全局变量，叫做ivim_github.

#### let g:ivim_default_scheme='gruvbox'
定义一个叫做ivim_default_scheme的变量，用作保存默认主题的名字。

#### let g:ivim_fancy_font=1
定义一个全局变量，用于记录是否启用多彩字体，1代表启用，0代表不启用。

#### let g:ivim_show_number=1
定义一个全局变量，用于记录是否启用行号，1代表启用，0代表不启用

#### let g:ivim_autocomplete='NEO'
定义一个全局变量，用于记录采用哪种自动补全功能，可以设置为YCM 或者 NEO。

#### let g:ivim_bundle_groups=['ui', 'enhance', 'move', 'navigate','complete', 'compile', 'git', 'language']
这里定义了一个全局的字符串数组，实际上是将所有的插件分成了8类，并且给每一个类起一个名字。


#### filereadable
个性化的设置, filereadable用于检查文件的可读性，expand函数用于扩展特殊的关键字。
vim中的相关函数可以参考这篇文章:http://blog.chinaunix.net/uid-24993824-id-160089.html
```shell
	if filereadable(expand($HOME . '/.vimrc.ivim.local'))
		source $HOME/.vimrc.ivim.local
	endif
```
这段代码的意思就是说如果$HOME/.vimrc.ivim.local可读的话，就执行source命令，使其生效。

## 通用设置

#### set nocompatible

是否兼容Vi，compatible为兼容，nocompatible为不完全兼容, 如果设置为compatible，则tab将不会变成空格 

#### filetype plugin indent on 
启用文件类型，1.自动识别文件类型    2.用文件类型plugin脚本   3.使用缩进定义文件

#### let mapleader=',' 
mapleader就是一个前缀，输入命令时要加这个前缀<leader>，在这里就是将<Leader>指定成逗号。

#### let maplocalleader='\' 
在一个全局插件里应该使用 <Leader> 而在一个文件类型插件里应该用 <LocalLeader>

#### set timeoutlen=500
输入一个命令的等待时间

#### autocmd BufWritePost \$MYVIMRC source \$MYVIMRC
在修改保存vimrc文件后自动应用这个文件中的配置

#### nnoremap \<Leader\>x :tabedit \$MYVIMRC\<CR\>
编辑vimrc的快捷键
#### set autoread
如果文件在vim外部被修改，则自动读取最新的结果
#### set autowrite
档案一有更动就会自动存盘

#### set hidden
允许在有未保存的修改时切换缓冲区

#### set history=1000
设置vim记录的命令的历史个数
#### set modeline
Vim 有能力去解读所编辑档案内的 Vim 控制参数，而这些控制参数是写在档案的批注行里头的

#### set encoding=utf-8
使用utf-8编码
#### set completeopt+=longest
优化自动补全
#### set completeopt-=preview
优化自动补全

#### set undofile 
设置撤销文件缓存区
```shell
function! InitializeDirectories()
    let parent=$HOME
    let prefix='.vim'
    let dir_list={
                \ 'backup': 'backupdir',
                \ 'view': 'viewdir',
                \ 'swap': 'directory',
                \ 'undo': 'undodir',
                \ 'cache': '',
                \ 'session': ''}
    for [dirname, settingname] in items(dir_list)
        let directory=parent.'/'.prefix.'/'.dirname.'/'
        if !isdirectory(directory)
            if exists('*mkdir')
                let dir = substitute(directory, "/$", "", "")
                call mkdir(dir, 'p')
            else
                echo 'Warning: Unable to create directory: '.directory
            endif
        endif
        if settingname!=''
            exe 'set '.settingname.'='.directory
        endif
    endfor
endfunction
call InitializeDirectories()

autocmd BufWinLeave *.* silent! mkview 
autocmd BufWinEnter *.* silent! loadview
```
## 针对操作系统的设置
#### 在Windows操作系统中，配置.vim
```shell
if has('win32') || has('win64')
    set runtimepath=$HOME/.vim,$VIM/vimfiles,$VIMRUNTIME,$VIM/vimfiles/after,$HOME/.vim/after
endif
```

#### 兼容Linux
`set viewoptions+=slash,unix`
#### 响应键盘映射更改
`set viewoptions-=options` 

## Vim的插件

#### 检查plug.vim
ivim使用的是plug.vim作为插件管理工具，所以需要先检查一下有没有安装plug.vim，如果没有安装，就会自动安装一个。
```shell
if empty(glob('~/.vim/autoload/plug.vim'))
    silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
                \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
    autocmd VimEnter * PlugInstall | source $MYVIMRC
endif
call plug#begin('~/.vim/bundle')
```

#### 添加UI界面的插件
count函数用于计算某值出现了多少次，这里就是判断是否存在ui这个字符串。
```shell
if count(g:ivim_bundle_groups, 'ui')
    Plug 'kristijanhusak/vim-hybrid-material' " hybrid material主题
    Plug 'morhetz/gruvbox' " gruvbox主题
    Plug 'jacoborus/tender.vim' " tender主题
    Plug 'vim-airline/vim-airline' | Plug 'vim-airline/vim-airline-themes' " 状态行插件
    Plug 'Yggdroot/indentLine' " 缩进级别
    Plug 'ryanoasis/vim-devicons' " 这是一个图标的插件
    Plug 'bling/vim-bufferline' " 显示Buffer行的插件
    Plug 'mhinz/vim-startify' " 开始页插件
endif
```

#### 添加Vim增强功能的插件
```shell
if count(g:ivim_bundle_groups, 'enhance')
    Plug 'Raimondi/delimitMate' " 自动补齐引号
    Plug 'tomtom/tcomment_vim' " 注释
    Plug 'tpope/vim-abolish' " 这个插件是可以快速替换字符串的
    Plug 'tpope/vim-speeddating' " 快速插入日期
    Plug 'tpope/vim-repeat' " 重复一个插件的操作
    Plug 'terryma/vim-multiple-cursors' " 多重光标选取功能，可以对单词进行批量改动
    Plug 'mbbill/undotree', { 'on': 'UndotreeToggle' } " 可以以一棵树的形式看到你的undo和redo历史
    Plug 'tpope/vim-surround' " 可以方便的在字符两边插入各种符号,比如单/双引号,大中小括弧等
    Plug 'junegunn/vim-easy-align', { 'on': ['<Plug>(EasyAlign)', 'EasyAlign'] } " 对齐插件
    Plug 'ludovicchabant/vim-gutentags' " 管理tags
    Plug 'AndrewRadev/splitjoin.vim' " 我们在写代码的时候，既可能将某个单行语句打散成多行语句，也可能将多行语句组合成单行语句。如果你是 Vim 用户，那么可以利用 Splitjoin 这个 Vim 插件来达到上述目的。
    Plug 'sickill/vim-pasta' " 增强复制粘贴的功能，使文本能够自动适应格式
    Plug 'Keithbsmiley/investigate.vim' " 帮助插件
    Plug 'wellle/targets.vim' " Text objects
    Plug 'roman/golden-ratio' " 窗口大小的调整
    Plug 'chrisbra/vim-diff-enhanced' " vim-diff的增强版
endif
```

#### 添加与位置移动相关的插件
```shell
if count(g:ivim_bundle_groups, 'move') 
    Plug 'tpope/vim-unimpaired' " 将一对符号内(如一对逗号内:,some text,)的字符看成一个对象进行操作
    Plug 'Lokaltog/vim-easymotion' " 快速跳转插件
    Plug 'unblevable/quick-scope' " Quick-Scope 是一个用于 Vim 的快速左右移动插件。当在一行中移动时，f 和 F，t 和 T 会关联起来。Quick-Scope 修复了它们唯一的缺点：很难一致选择正确的字符目标。
    Plug 'bkad/CamelCaseMotion' " Camel case motion
    Plug 'majutsushi/tagbar' " Tag bar
    Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all'  } " Fuzzy finder
    Plug 'junegunn/fzf.vim' " Fuzzy finder plugin
endif
```

#### 添加用于文件导航的插件
```shell
if count(g:ivim_bundle_groups, 'navigate') 
    Plug 'scrooloose/nerdtree', { 'on': 'NERDTreeToggle' } " NERD 树形目录
    Plug 'Xuyuanp/nerdtree-git-plugin', { 'on': 'NERDTreeToggle' } " NERD 树形Git插件
    Plug 'mhinz/vim-tmuxify' " tmux是一个用于终端复用的软件，
                             " 它允许一个用户在一个终端窗口或远程终端会话中使用多个不同的终端会话。
                             " 在同一个命令行接口处理多个程序，以及将程序从已经开始运行另外的程序的Unix shell中分离出来，是非常有用的。
endif
```

#### 添加代码补全功能的插件
```shell
if count(g:ivim_bundle_groups, 'complete') " Completion
    if g:ivim_autocomplete=='NEO'
        if has('lua')
            let g:ivim_completion_engine='neocomplete'
            Plug 'Shougo/neocomplete.vim' " Auto completion framework
        else
            let g:ivim_completion_engine='neocomplcache'
            Plug 'Shougo/neocomplcache.vim' " Auto completion framework
        endif
        Plug 'Shougo/neosnippet.vim' " Snippet engine
        Plug 'Shougo/neosnippet-snippets' " Snippets
        Plug 'Shougo/vimproc.vim', { 'do': 'make' }
        Plug 'wellle/tmux-complete.vim' " Completion for tmux panes
    else
        " Auto completion framework
        let g:ivim_completion_engine='YouCompleteMe'
        Plug 'Valloric/YouCompleteMe', { 'do': './install.py' } "Auto completion framework
        Plug 'honza/vim-snippets' " Snippets
        Plug 'sirver/ultisnips' " Snippet engine
    endif
endif
```

#### 添加与编译、语法高亮相关的插件
```shell
if count(g:ivim_bundle_groups, 'compile') " Compiling
    Plug 'scrooloose/syntastic' " Syntax checking
    Plug 'xuhdev/SingleCompile' " Single compile
endif
```
#### 添加与Git相关的插件
```shell
if count(g:ivim_bundle_groups, 'git') " Git
    Plug 'tpope/vim-fugitive' " Git wrapper
    Plug 'gregsexton/gitv' " Gitk clone
    if has('signs')
        Plug 'airblade/vim-gitgutter' " Git diff sign
    endif
endif
```

#### 添加与编程语言支持相关的插件
```shell
if count(g:ivim_bundle_groups, 'language') 
    Plug 'davidhalter/jedi-vim', { 'for': 'python' } " Python jedi plugin
    Plug 'tpope/vim-rails', { 'for': [] } " Rails
    Plug 'mattn/emmet-vim', { 'for': ['html', 'css'] } " Emmet
    Plug 'sheerun/vim-polyglot' " Language Support
endif
```

#### 添加本地插件
```shell
if filereadable(expand($HOME . '/.vimrc.bundles.local')) " Load local bundles
    source $HOME/.vimrc.bundles.local
endif
call plug#end()
```

## UI相关的设置

![界面示意图](http://upload-images.jianshu.io/upload_images/322965-0f95d930181572c7.png?imageMogr2/auto-orient/strip%7CimageView2/2)

```shell
if count(g:ivim_bundle_groups, 'ui')
    let g:airline#extensions#tabline#enabled=1
else
    " 设置airline的标题
    set title
    set titlestring=%t%(\ %m%)%(\ (%{expand('%:p:h')})%)%(\ %a%)
    
    " 设置tabline
    set showtabline=2 " Always show tab line
    " 设置tab标签
    set guitablabel=%m%N:%t[%{tabpagewinnr(v:lnum)}]
    set tabline=%!MyTabLine()
    function! MyTabLine()
        let s=''
        let t=tabpagenr() " The index of current page
        let i=1
        while i<=tabpagenr('$') " From the first page
            let buflist=tabpagebuflist(i)
            let winnr=tabpagewinnr(i)
            let s.=(i==t ? '%#TabLineSel#' : '%#TabLine#')
            let s.='%'.i.'T'
            let s.=' '
            let bufnr=buflist[winnr-1]
            let file=bufname(bufnr)
            let buftype = getbufvar(bufnr, 'buftype')
            let m=''
            if getbufvar(bufnr, '&modified')
                let m='[+]'
            endif
            if buftype=='nofile'
                if file=~'\/.'
                    let file=substitute(file, '.*\/\ze.', '', '')
                endif
            else
                let file=fnamemodify(file, ':p:t')
            endif
            if file==''
                let file='[No Name]'
            endif
            let s.=m
            let s.=i.':'
            let s.=file
            let s.='['.winnr.']'
            let s.=' '
            let i=i+1
        endwhile
        let s.='%T%#TabLineFill#%='
        let s.=(tabpagenr('$')>1 ? '%999XX' : 'X')
        return s
    endfunction
    " 设置airline的主题
    if g:ivim_default_scheme=='gruvbox'
        let g:gruvbox_invert_tabline=1
    endif
    " 设置tab: Set up tab tooltips with each buffer name
    set guitabtooltip=%F
endif

" Set status line
if count(g:ivim_bundle_groups, 'ui')
    set laststatus=2 " Show the statusline
    set noshowmode " Hide the default mode text
    " Set status line colorscheme
    if g:ivim_default_scheme=='hybrid'
        let g:airline_theme='bubblegum'
    elseif g:ivim_default_scheme=='tender'
        let g:tender_airline=1
        let g:airline_theme='tender'
    endif
    set ttimeoutlen=50
    let g:bufferline_echo=0
    let g:bufferline_modified='[+]'
    if g:ivim_fancy_font
        let g:airline_powerline_fonts=1
    else
        let g:airline_left_sep=''
        let g:airline_right_sep=''
    endif
endif

" Only have cursorline in current window and in normal window
autocmd WinLeave * set nocursorline
autocmd WinEnter * set cursorline
autocmd InsertEnter * set nocursorline
autocmd InsertLeave * set cursorline
set wildmenu " Show list instead of just completing
set wildmode=list:longest,full " Use powerful wildmenu
set shortmess=at " Avoids hit enter
set showcmd " Show cmd

set backspace=indent,eol,start " Make backspaces delete sensibly
set whichwrap+=h,l,<,>,[,] " Backspace and cursor keys wrap to
set virtualedit=block,onemore " Allow for cursor beyond last character
set scrolljump=5 " Lines to scroll when cursor leaves screen
set scrolloff=3 " Minimum lines to keep above and below cursor
set sidescroll=1 " Minimal number of columns to scroll horizontally
set sidescrolloff=10 " Minimal number of screen columns to keep away from cursor

set showmatch " Show matching brackets/parenthesis
set matchtime=2 " Decrease the time to blink

if g:ivim_show_number
    set number " Show relative numbers
    " 显示相对行号，<Leader>键就是逗号键
    nnoremap <Leader>n :set relativenumber!<CR>
endif

set formatoptions+=rnlmM " Optimize format options
set wrap " Set wrap
set textwidth=80 " Change text width
if g:ivim_fancy_font
    set list " Show these tabs and spaces and so on
    set listchars=tab:▸\ ,eol:¬,extends:❯,precedes:❮ " Change listchars
    set linebreak " Wrap long lines at a blank
    set showbreak=↪  " Change wrap line break
    set fillchars=diff:⣿,vert:│ " Change fillchars
    augroup trailing " Only show trailing whitespace when not in insert mode
        autocmd!
        autocmd InsertEnter * :set listchars-=trail:⌴
        autocmd InsertLeave * :set listchars+=trail:⌴
    augroup END
endif

" Set gVim UI setting
if has('gui_running')
    set guioptions-=m
    set guioptions-=T
endif
```

##  颜色和字体设置
#### syntax on
Vim会自动检测到你的文件类型并为之载入相应的语法高亮

#### set background=dark
设置界面的底色为暗色，并且采用256中颜色
```shell
if !has('gui_running')
    set t_Co=256 " Use 256 colors
endif
```
#### 设置终端配色
```shell
if (empty($TMUX))
    if (has("termguicolors"))
        set termguicolors
    endif
endif
```

#### 设置终端的颜色主题
```shell
if count(g:ivim_bundle_groups, 'ui')
    if g:ivim_default_scheme=='hybrid'
        colorscheme hybrid_reverse
    elseif g:ivim_default_scheme=='gruvbox'
        colorscheme gruvbox
    elseif g:ivim_default_scheme=='tender'
        colorscheme tender
    endif
else
    colorscheme desert
endif
```

#### 设置界面字体
```shell
if has('gui_running')
    if has('gui_gtk')
        set guifont=DejaVu\ Sans\ Mono\ 18
    else
        set guifont=DejaVu\ Sans\ Mono:h18
    endif
endif
```
## 与缩进相关的设置

#### set autoindent 
设置自动缩进
#### set cindent 
缩进风格C语言的风格

#### set expandtab
将tab符号全部转换成空格
#### set softtabstop=4 
设置缩进级别
#### set shiftwidth=4
每次缩进的列数
#### set shiftround 
Indent/outdent to nearest tabstop

##与搜索相关的设置

set ignorecase " Case insensitive search
set smartcase " Case sensitive when uc present
set hlsearch " Highlight search terms
set incsearch " Find as you type search
set gdefault " turn on g flag

#### 使用正则表达式的快捷键
nnoremap / /\v
vnoremap / /\v
cnoremap s/ s/\v
nnoremap ? ?\v
vnoremap ? ?\v
cnoremap s? s?\v

#### 搜索下一项
nnoremap n nzzzv
nnoremap N Nzzzv
nnoremap * *zzzv
nnoremap # #zzzv
nnoremap g* g*zzzv
nnoremap g# g#zzzv

#### 可视化的搜索
```shell
function! s:VSetSearch()
    let temp=@@
    normal! gvy
    let @/='\V' . substitute(escape(@@, '\'), '\n', '\\n', 'g')
    let @@=temp
endfunction
vnoremap * :<C-U>call <SID>VSetSearch()<CR>//<CR>
vnoremap # :<C-U>call <SID>VSetSearch()<CR>??<CR>
```

####  启用搜索结果高亮
nnoremap <Leader><Space> :set hlsearch!<CR>

## 与目录相关的设置

set foldlevelstart=0 " Start with all folds closed
set foldcolumn=1 " Set fold column

#### 创建目录
nnoremap <silent> <Space> @=(foldlevel('.') ? 'za' : '\<Space>')<CR>
vnoremap <Space> zf

#### Set foldtext
```shell
function! MyFoldText()
    let line=getline(v:foldstart)
    let nucolwidth=&foldcolumn+&number*&numberwidth
    let windowwidth=winwidth(0)-nucolwidth-3
    let foldedlinecount=v:foldend-v:foldstart+1
    let onetab=strpart('          ', 0, &tabstop)
    let line=substitute(line, '\t', onetab, 'g')
    let line=strpart(line, 0, windowwidth-2-len(foldedlinecount))
    let fillcharcount=windowwidth-len(line)-len(foldedlinecount)
    return line.'…'.repeat(' ',fillcharcount).foldedlinecount.'L'.' '
endfunction
set foldtext=MyFoldText()
```
## 快捷键设置


#### hjkl键的重定义
`nnoremap j gj`
`nnoremap k gk`
`vnoremap j gj`
`vnoremap k gk`

#### 在窗口之间跳转
`nnoremap <C-J> <C-W>j`
`nnoremap <C-K> <C-W>k`
`nnoremap <C-H> <C-W>h`
`nnoremap <C-L> <C-W>l`

#### Same when jumping around
`nnoremap g; g;zz`
`nnoremap g, g,zz`

#### Reselect visual block after indent/outdent
`vnoremap < <gv`
`vnoremap > >gv`

#### Repeat last substitution, including flags, with &.
`nnoremap & :&&<CR>`
`xnoremap & :&&<CR>`

#### Keep the cursor in place while joining lines
`nnoremap J mzJ`z`

#### Select entire buffer
`nnoremap vaa ggvGg_`

#### Strip all trailing whitespace in the current file
`nnoremap <Leader>q :%s/\s\+$//<CR>:let @/=''<CR>`

#### 修改所有的缩进
`nnoremap \= gg=G`

#### See the differences between the current buffer and the file it was loaded from
```shell
command! DiffOrig vert new | set bt=nofile | r ++edit # | 0d_
            \ | diffthis | wincmd p | diffthis
```

## 插件设置

#### 设置界面相关插件的参数
```shell
if count(g:ivim_bundle_groups, 'ui')
    " -> Startify
    let g:startify_session_dir=$HOME . '/.vim/session'
    let g:startify_custom_header=['', '欢迎使用iVim编辑器', 'iVim原作者使张晓鸥，由汪海伟重新翻译整理']
    let g:startify_custom_footer=['', '尽情享用吧']
    if has('gui_running')
        hi StartifyHeader  guifg=#87afff
        hi StartifyFooter  guifg=#87afff
        hi StartifyBracket guifg=#585858
        hi StartifyNumber  guifg=#ffaf5f
        hi StartifyPath    guifg=#8a8a8a
        hi StartifySlash   guifg=#585858
    else
        hi StartifyHeader  ctermfg=111
        hi StartifyFooter  ctermfg=111
        hi StartifyBracket ctermfg=240
        hi StartifyNumber  ctermfg=215
        hi StartifyPath    ctermfg=245
        hi StartifySlash   ctermfg=240
    endif
    
    " Goyo插件，用作者的描述就是：Distraction-free writing in Vim. 不会被任何的其他元素打扰
    autocmd! User GoyoEnter Limelight
    autocmd! User GoyoLeave Limelight!

endif
```

#### 设置功能增强型插件的参数
```shell
if count(g:ivim_bundle_groups, 'enhance')
    " -> delimitMate
    let delimitMate_expand_cr=1
    let delimitMate_expand_space=1
    let delimitMate_balance_matchpairs=1
    
    " -> Tcomment
    " Map \<Space> to commenting
    function! IsWhiteLine()
        if (getline('.')=~'^$')
            exe 'TCommentBlock'
            normal! j
        else
            normal! A   
            exe 'TCommentRight'
            normal! l
            normal! x
        endif
        startinsert!
    endfunction
    nnoremap <silent> <LocalLeader><Space> :call IsWhiteLine()<CR>
    
    " -> Multiple cursors
    " Called once right before you start selecting multiple cursors
    if g:ivim_autocomplete=='NEO'
        function! Multiple_cursors_before()
            if g:ivim_completion_engine=='neocomplete'
                exe 'NeoCompleteLock'
            else
                exe 'NeoComplCacheLock'
            endif
        endfunction
        " Called once only when the multiple selection is canceled (default <Esc>)
        function! Multiple_cursors_after()
            if g:ivim_completion_engine=='neocomplete'
                exe 'NeoCompleteUnlock'
            else
                exe 'NeoComplCacheUnlock'
            endif
        endfunction
    endif
    
    " -> Undo tree
    nnoremap <Leader>u :UndotreeToggle<CR>
    let g:undotree_SetFocusWhenToggle=1
    
    " -> Easy Align
    xmap ga <Plug>(EasyAlign)
    nmap ga <Plug>(EasyAlign)
    
    " -> Gutentags
    let g:gutentags_cache_dir=$HOME . '/.vim/cache/ctags'
    
    " -> Splitjoin
    let g:splitjoin_split_mapping = ',s'
    let g:splitjoin_join_mapping  = ',j'
    let g:splitjoin_normalize_whitespace=1
    let g:splitjoin_align=1
    
    " -> Investigate.vim
    nnoremap K :call investigate#Investigate()<CR>
    let g:investigate_use_dash=1
    
    " -> EnhancedDiff
    let &diffexpr='EnhancedDiff#Diff("git diff", "--diff-algorithm=patience")'
endif
```

#### 设置位置移动相关插件的参数
```shell
if count(g:ivim_bundle_groups, 'move')
    " -> Tag bar
    nnoremap <Leader>t :TagbarToggle<CR>
    let g:tagbar_autofocus=1
    let g:tagbar_expand=1
    let g:tagbar_foldlevel=2
    let g:tagbar_autoshowtag=1
    
    " Matchit
    " Start mathit
    packadd! matchit
    " Use Tab instead of % to switch
    nmap <Tab> %
    vmap <Tab> %

endif
```

#### 设置目录导航相关插件的参数
```shell
if count(g:ivim_bundle_groups, 'navigate')
    " -> NERD Tree
    nnoremap <Leader>f :NERDTreeToggle<CR>
    let NERDTreeChDirMode=2
    let NERDTreeShowBookmarks=1
    let NERDTreeShowHidden=1
    let NERDTreeShowLineNumbers=1
    augroup nerd_loader
        autocmd!
        autocmd VimEnter * silent! autocmd! FileExplorer
        autocmd BufEnter,BufNew *
                    \  if isdirectory(expand('<amatch>'))
                    \|   call plug#load('nerdtree')
                    \|   execute 'autocmd! nerd_loader'
                    \| endif
    augroup END
endif
```

#### 设置代码补全功能插件的参数
```shell
if count(g:ivim_bundle_groups, 'complete')

    if g:ivim_autocomplete=='NEO'
        " -> Neocomplete & Neocomplcache
        " Use Tab and S-Tab to select candidate
        inoremap <expr><Tab>  pumvisible() ? "\<C-N>" : "\<Tab>"
        inoremap <expr><S-Tab> pumvisible() ? "\<C-P>" : "\<S-Tab>"
        if g:ivim_completion_engine=='neocomplete'
            let g:neocomplete#enable_at_startup=1
            let g:neocomplete#data_directory=$HOME . '/.vim/cache/neocomplete'
            let g:neocomplete#enable_auto_delimiter=1
            " Use <C-E> to close popup
            inoremap <expr><C-E> neocomplete#cancel_popup()
            inoremap <expr><CR> delimitMate#WithinEmptyPair() ?
                        \ "\<C-R>=delimitMate#ExpandReturn()\<CR>" :
                        \ pumvisible() ? neocomplete#close_popup() : "\<CR>"
        else
            let g:neocomplcache_enable_at_startup=1
            let g:neocomplcache_temporary_dir=$HOME . '/.vim/cache/neocomplcache'
            let g:neocomplcache_enable_auto_delimiter=1
            let g:neocomplcache_enable_fuzzy_completion=1
            " Use <C-E> to close popup
            inoremap <expr><C-E> neocomplcache#cancel_popup()
            inoremap <expr><CR> delimitMate#WithinEmptyPair() ?
                        \ "\<C-R>=delimitMate#ExpandReturn()\<CR>" :
                        \ pumvisible() ? neocomplcache#close_popup() : "\<CR>"
        endif
```

#### 设置特定编程语言插件的参数
```shell
        if has('lua')
            if !exists('g:neocomplete#force_omni_input_patterns')
                let g:neocomplete#force_omni_input_patterns={}
            endif
            let g:neocomplete#force_omni_input_patterns.python=
            \ '\%([^. \t]\.\|^\s*@\|^\s*from\s.\+import \|^\s*from \|^\s*import \)\w*'
        else
            if !exists('g:neocomplcache_force_omni_patterns')
                let g:neocomplcache_force_omni_patterns={}
            endif
            let g:neocomplcache_force_omni_patterns.python=
            \ '\%([^. \t]\.\|^\s*@\|^\s*from\s.\+import \|^\s*from \|^\s*import \)\w*'
        endif
        autocmd FileType python setlocal omnifunc=jedi#completions
        let g:jedi#completions_enabled=0
        let g:jedi#auto_vim_configuration=0
        let g:jedi#smart_auto_mappings=0
        let g:jedi#use_tabs_not_buffers=1
        let g:tmuxcomplete#trigger=''
        " -> Neosnippet
        " Set information for snippets
        let g:neosnippet#enable_snipmate_compatibility=1
        " Use <C-K> to expand or jump snippets in insert mode
        imap <C-K> <Plug>(neosnippet_expand_or_jump)
        " Use <C-K> to replace TARGET within snippets in visual mode
        xmap <C-K> <Plug>(neosnippet_start_unite_snippet_target)
        " For snippet_complete marker
        if has('conceal')
            set conceallevel=2 concealcursor=i
        endif
    else
        " -> UltiSnips
        let g:UltiSnipsExpandTrigger="<C-K>"
        let g:UltiSnipsJumpForwardTrigger="<Tab>"
        let g:UltiSnipsJumpBackwardTrigger="<S-Tab>"
    endif
    let g:snips_author=g:ivim_user
    let g:snips_email=g:ivim_email
    let g:snips_github=g:ivim_github

endif
```

#### 设置编译、语法提示相关插件的参数
```shell
if count(g:ivim_bundle_groups, 'compile')

    " -> Syntastic
    let g:syntastic_check_on_open=1
    let g:syntastic_aggregate_errors=1
    let g:syntastic_auto_jump=1
    let g:syntastic_auto_loc_list=1
    if g:ivim_fancy_font
        let g:syntastic_error_symbol = '✗'
        let g:syntastic_style_error_symbol = '✠'
        let g:syntastic_warning_symbol = '∆'
        let g:syntastic_style_warning_symbol = '≈'
    endif
    
    " -> Singlecompile
    nnoremap <Leader>r :SingleCompileRun<CR>
    let g:SingleCompile_showquickfixiferror=1

endif

" Setting for git plugins
if count(g:ivim_bundle_groups, 'git')
endif

" Setting for language specificity
if count(g:ivim_bundle_groups, 'language')

    " -> Emmet
    let g:user_emmet_leader_key='<C-Z>'
    let g:user_emmet_settings={'indentation':'    '}
    let g:use_emmet_complete_tag=1

endif
```

## 本地设置
####如果存在本地的vimrc配置文件，则启用这个本地配置
```shell
if filereadable(expand($HOME . '/.vimrc.local'))
    source $HOME/.vimrc.local
endif
```

#### Use local gvimrc if available and gui is running
```shell
if has('gui_running')
    if filereadable(expand($HOME . '/.gvimrc.local'))
        source $HOME/.gvimrc.local
    endif
endif
```

